#include "MVariables.pmc"
#include "PVariables.pmc"
#include "Constants.pmc"
#include "Timers.pmc"



// the following spline calculation is the pmac equivalent of the below c++ code
// (it is easier for me to look at the c++...)
// The first part is setting the coefficients
;;;;      T const dydx0 = (fY[1] - fY[0]) / (fX[1] - fX[0]);
;;;;      fYPP[0] = T(-0.5);
;;;;      u[0] = T(0)
;;;;
;;;;      for (int i = 1; i < N-1; ++i) {
;;;;        sig = (fX[i] - fX[i-1]) / (fX[i+1] - fX[i-1]);
;;;;        p = sig * fYPP[i-1] + T(2);
;;;;        fYPP[i] = (sig - 1.) / p;
;;;;        u[i] = (fY[i+1] - fY[i]) / (fX[i+1] - fX[i]) - (fY[i] - fY[i-1]) / (fX[i] - fX[i-1]);
;;;;        u[i] = (6. * u[i] / (fX[i+1] - fX[i-1]) - sig * u[i-1]) / p;
;;;;      }
;;;;
;;;;      T const dydxN = (fY[N-1] - fY[N-2]) / (fX[N-1] - fX[N-2]);
;;;;
;;;;      double qn = 0.5;
;;;;      T un = (3. / (fX[N-1] - fX[N-2])) * (dydxN - (fY[N-1] - fY[N-2]) / (fX[N-1] - fX[N-2]));
;;;;      fYPP[N-1] = (un - qn * u[N-2]) / (qn * fYPP[N-2] + T(1.));
;;;;
;;;;      for (int k = N-2; k >= 0; --k) {
;;;;        fYPP[k] = fYPP[k] * fYPP[k+1] + u[k];
;;;;      }
// The second part is getting a value
;;;;      int klo=0;
;;;;      int khi = (int) fX.size() - 1;
;;;;      int k;
;;;;      while (khi - klo > 1) {
;;;;        k = (khi + klo) >> 1;
;;;;        if (fX[k] > x) {
;;;;          khi = k;
;;;;        } else {
;;;;          klo = k;
;;;;        }
;;;;      }
;;;;
;;;;      double const h = fX[khi] - fX[klo];
;;;;      double const a = (fX[khi] - x) / h;
;;;;      double const b = (x - fX[klo]) / h;
;;;;
;;;;      return a * fY[klo] + b * fY[khi] + ((a * a * a - a) * fYPP[klo] + (b * b * b - b) * fYPP[khi]) * (h * h) / 6.;



// Local define only
// Used for setting and in loop.
// These are for index 0 (lX0), index i (lXI), inde i+1 (lXIP1), index i-1 (lXIM1)
// Same applies to Y, YPP, U, etc
#define lUX0 P(kSplineUXStart)
#define lUY0 P(kSplineUYStart)
#define lX0 P(kSplineXStart)
#define lY0 P(kSplineYStart)
#define lXPP0 P(kSplineXPPStart)
#define lYPP0 P(kSplineYPPStart)
#define lX1 P(kSplineXStart + 1)
#define lY1 P(kSplineYStart + 1)
#define lUXI P(kSplineUXStart + P_INDEX)
#define lUYI P(kSplineUYStart + P_INDEX)
#define lXI P(kSplineXStart + P_INDEX)
#define lYI P(kSplineYStart + P_INDEX)
#define lXPPI P(kSplineXPPStart + P_INDEX)
#define lYPPI P(kSplineYPPStart + P_INDEX)
#define lXIP1 P(kSplineXStart + P_INDEX + 1)
#define lYIP1 P(kSplineYStart + P_INDEX + 1)
#define lXPPIP1 P(kSplineXPPStart + P_INDEX + 1)
#define lYPPIP1 P(kSplineYPPStart + P_INDEX + 1)
#define lUXIM1 P(kSplineUXStart + P_INDEX - 1)
#define lUYIM1 P(kSplineUYStart + P_INDEX - 1)
#define lXIM1 P(kSplineXStart + P_INDEX - 1)
#define lYIM1 P(kSplineYStart + P_INDEX - 1)
#define lXPPIM1 P(kSplineXPPStart + P_INDEX - 1)
#define lYPPIM1 P(kSplineYPPStart + P_INDEX - 1)

// Default spline data is set here
P620=   100.000    P640=   16180.575
P621=   157.895    P641=   20511.413
P622=   215.789    P642=   24406.112
P623=   273.684    P643=   27137.775
P624=   331.579    P644=   29537.384
P625=   389.474    P645=   31660.283
P626=   447.368    P646=   33531.471
P627=   505.263    P647=   35331.942
P628=   563.158    P648=   36984.597
P629=   621.053    P649=   38605.412
P630=   678.947    P650=   40170.510
P631=   736.842    P651=   41736.350
P632=   794.737    P652=   43302.614
P633=   852.632    P653=   44930.645
P634=   910.526    P654=   46590.920
P635=   968.421    P655=   48368.501
P636=  1026.316    P656=   50258.585
P637=  1084.211    P657=   52283.675
P638=  1142.105    P658=   54678.711
P639=  1200.000    P659=   57212.478


/***
    This PLC calculates the spline coefficients.
    2 splines are calculated because you need both the
    forward and the inverse for the kinematics

    X - Energy in eV
    Y - Gap in um
***/

Open PLC kPLC_FlySplineCalc Clear

// If the fly CS is running a program ignore this request
if (M_CS6Running = 0)

  // Start with spline not OK and no ERROR and InCalc
  P_SplineOK = 0
  P_SplineERROR = 0
  P_SplineInCalc = 1


  // Check both X and Y are monotonic increasing 1-1 data
  P_INDEX = 0
  while (P_INDEX < kNSPLINE - 1)
    if (lXIP1 < lXI or lYIP1 < lYI)
      P_SplineERROR = 1
    endif
    P_INDEX = P_INDEX + 1
  endw

  // Calculate derivatives
  P_DYDX = (lY1 - lY0) / (lX1 - lX0)
  P_DXDY = 1 / P_DYDX
  lYPP0 = -0.5
  lXPP0 = -0.5
  lUY0 = 0
  lUX0 = 0

  P_INDEX = 1
  while (P_INDEX < kNSPLINE - 1)
    P_SIGY = (lXI - lXIM1) / (lXIP1 - lXIM1)
    P_SIGX = (lYI - lYIM1) / (lYIP1 - lYIM1)
    P_PY = P_SIGY * lYPPIM1 + 2
    P_PX = P_SIGX * lXPPIM1 + 2
    lYPPI = (P_SIGY - 1.) / P_PY
    lXPPI = (P_SIGX - 1.) / P_PX
    lUYI = (lYIP1 - lYI) / (lXIP1 - lXI) - (lYI - lYIM1) / (lXI - lXIM1)
    lUXI = (lXIP1 - lXI) / (lYIP1 - lYI) - (lXI - lXIM1) / (lYI - lYIM1)
    lUYI = (6 * lUYI / (lXIP1 - lXIM1) - P_SIGY * lUYIM1) / P_PY
    lUXI = (6 * lUXI / (lYIP1 - lYIM1) - P_SIGX * lUXIM1) / P_PX
    P_INDEX = P_INDEX + 1
  endwhile

  P_INDEX = kNSPLINE - 1
  P_DYDXN = (lYI - lYIM1) / (lXI - lXIM1)
  P_DXDYN = 1 / P_DYDXN
  P_QNY = 0.5
  P_QNX = 0.5
  P_UNY = 3. / (lXI - lXIM1) * (P_DYDXN - (lYI - lYIM1) / (lXI - lXIM1))
  P_UNX = 3. / (lYI - lYIM1) * (P_DXDYN - (lXI - lXIM1) / (lYI - lYIM1))
  lYPPI = (P_UNY - P_QNY * lUYIM1) / (P_QNY * lYPPIM1 + 1)
  lXPPI = (P_UNX - P_QNX * lUXIM1) / (P_QNX * lXPPIM1 + 1)

  P_INDEX = kNSPLINE - 2
  while (P_INDEX > -1)
    lYPPI = lYPPI * lYPPIP1 + lUYI
    lXPPI = lXPPI * lXPPIP1 + lUXI
    P_INDEX = P_INDEX - 1
  endwhile

endif

if (P_SplineERROR = 0)
  P_SplineOK = 1
endif

P_SplineInCalc = kFALSE
Disable PLC 6
Close
